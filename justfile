import 'tools.just'

_default:
    @just --list --unsorted

[group('dev')]
fmt *args:
    cargo fmt {{args}}

[group('dev')]
lint:
    cargo clippy --release --locked -- -D warnings

[group('dev')]
test:
    cargo test --locked

# Check dependency sources, bans, and licenses against deny.toml
[group('dev')]
deny: (_require "cargo-deny")
    cargo deny --locked check sources bans licenses

# Check dependencies against the RustSec advisory database
[group('dev')]
audit: (_require "cargo-deny")
    cargo deny --locked check advisories

# Run all checks (before commit, push, merge, release)
[group('dev')]
@checks: (fmt "--check") lint test deny

[group('dev')]
run:
    cargo run --locked --bin mujina-minerd

# Update all dependencies, or only the named crates, to aged versions
[group('deps')]
update-deps *crates: (_require "cargo-cooldown")
    cargo cooldown update {{ prepend("-p ", crates) }}

# Bring Cargo.lock in line with Cargo.toml after a manifest edit
[group('deps')]
resolve-deps: (_require "cargo-cooldown")
    cargo cooldown check

[private]
_require tool:
    @command -v {{tool}} >/dev/null || { \
        echo "error: {{tool}} is not installed; run 'just setup-tools'" >&2; \
        exit 1; }

# Container engine for the recipes below. Podman is the default and
# what CI uses; Docker works too. Autodetected, so a Docker-only
# machine needs no configuration --- override with CONTAINER_ENGINE.
CONTAINER_ENGINE := env('CONTAINER_ENGINE', shell('command -v podman >/dev/null 2>&1 && echo podman || echo docker'))

# Rootless Podman maps the container's root to the invoking user, so
# files written into the bind mount stay owned by us. Docker (rootful)
# does not: without --user, everything cargo writes into target/ and
# .cache/ lands root-owned on the host and the next host-side build
# fails on permissions. Running as the host user then makes the image's
# root-owned /usr/local/cargo unwritable for cargo's package-cache
# lock, so point CARGO_HOME at the bind-mounted workspace instead.
# Left empty for Podman, whose existing behavior is unchanged.
CONTAINER_RUN_ARGS := if CONTAINER_ENGINE == "docker" {
        "--user " + shell('id -u') + ":" + shell('id -g') +
        " -e CARGO_HOME=/workspace/.cache/cargo-home"
    } else { "" }

BUILD_IMAGE := "mujina-build"
# These files decide the build toolchain image's content. tools.just
# qualifies because the image runs setup-tools from it.
IMAGE_INPUTS := "build.Containerfile tools.just"
# Tag with a content hash of the image inputs so we can detect
# staleness without rebuilding. This matters in CI where podman
# save/load doesn't preserve layer cache---podman build would
# rebuild from scratch even with a loaded image. The content-hash
# tag lets an `image inspect` probe skip the build entirely.
BUILD_TAG := shell('sha256sum ' + IMAGE_INPUTS + ' | sha256sum | cut -c1-12')

# Build the build toolchain image (skips if unchanged)
[group('container')]
build-image:
    {{CONTAINER_ENGINE}} image inspect {{BUILD_IMAGE}}:{{BUILD_TAG}} >/dev/null 2>&1 || \
        {{CONTAINER_ENGINE}} build -t {{BUILD_IMAGE}}:{{BUILD_TAG}} -f build.Containerfile .

# Remove stale build toolchain images
[group('container')]
build-image-clean:
    {{CONTAINER_ENGINE}} images --format '{{{{.Repository}}:{{{{.Tag}}' \
        | grep '^{{BUILD_IMAGE}}:' \
        | grep -v ':{{BUILD_TAG}}$' \
        | xargs -r {{CONTAINER_ENGINE}} rmi

# Run a just recipe inside the build toolchain image
[group('container')]
in-container *args: build-image
    mkdir -p .cache/cargo-registry .cache/cargo-git .cache/cargo-home
    {{CONTAINER_ENGINE}} run --rm \
        {{CONTAINER_RUN_ARGS}} \
        -v "$(pwd)":/workspace:Z \
        -v "$(pwd)/.cache/cargo-registry":/usr/local/cargo/registry \
        -v "$(pwd)/.cache/cargo-git":/usr/local/cargo/git \
        -w /workspace \
        {{BUILD_IMAGE}}:{{BUILD_TAG}} \
        just {{args}}

# Check every commit from base to HEAD individually inside the build
# toolchain container, or the working tree when there are no new
# commits. The default base prefers a remote named upstream over
# origin and uses that remote's default branch.
# The CI pipeline. This is what GitHub Actions runs.
[group('ci')]
ci base="auto":
    #!/usr/bin/env bash
    set -euo pipefail
    base="{{base}}"
    if [ "$base" = "auto" ]; then
        remote=$(git remote | grep -qx upstream && echo upstream || echo origin)
        base=$(git symbolic-ref -q --short "refs/remotes/$remote/HEAD" \
            || echo "$remote/main")
    fi
    commits=$(git rev-list --reverse "$base"..HEAD)
    if [ -z "$commits" ]; then
        exec just in-container checks
    fi
    if ! git diff --quiet || ! git diff --cached --quiet; then
        echo "error: working tree dirty; commit or stash first, or run" >&2
        echo "'just in-container checks' to check only the working tree" >&2
        exit 1
    fi
    orig=$(git rev-parse --abbrev-ref HEAD)
    [ "$orig" = "HEAD" ] && orig=$(git rev-parse HEAD)
    trap 'git checkout --quiet "$orig"' EXIT
    for sha in $commits; do
        echo "::group::$(git log --oneline --no-decorate -1 "$sha")"
        git checkout --quiet "$sha"
        just in-container checks
        echo "::endgroup::"
    done

[group('container')]
container-build tag=`git rev-parse --abbrev-ref HEAD`:
    {{CONTAINER_ENGINE}} build -t mujina-minerd:{{tag}} -f Containerfile .

[group('container')]
container-push tag=`git rev-parse --abbrev-ref HEAD`:
    {{CONTAINER_ENGINE}} tag mujina-minerd:{{tag}} ghcr.io/256foundation/mujina-minerd:{{tag}}
    {{CONTAINER_ENGINE}} push ghcr.io/256foundation/mujina-minerd:{{tag}}

# Configure git to use the project's .githooks directory
[group('setup')]
setup-hooks:
    git config core.hooksPath .githooks
    @echo "Git hooks configured to use .githooks/"
    @ls .githooks/ | sed 's/^/  - /'
